extends Бюджет_ЗПиДД.Рабочие.редНазначениеПособия "";

import classes Константы,ФункцииПримененияКостант;--,ФункцииДанных;


inobject private

  var кнЗаявкаФСС : Button;

--------------------------------------------------------------------------------
--{ Обработчики событий бланка

proc шаблон_ПриСоздании (Context: Variant);
  extended шаблон_ПриСоздании(Context);
  if (кнЗаявкаФСС = nil):
    кнЗаявкаФСС = СоздатьДопОбъект(Button, , 1);
    кнЗаявкаФСС.Caption = 'Заявка в ТО ФСС';
    кнЗаявкаФСС.Hint = 'Просмотреть/добавть заявку в ТО ФСС';
  fi;
end;


  proc шаблон_ПриСчитывании;
    extended  шаблон_ПриСчитывании;
    кнЗаявкаФСС.Visible = false;
    if (Интерфейс.СуммаОплатыФсс <> 0) :
      if (ЭтоПрямыеВыплаты(Self.Record.ВидСтраховогоОбеспечения.Код)) :
        кнЗаявкаФСС.Visible = true;
      fi;
    fi;
  end;

proc кнЗаявкаФСС_ПриНажатии (Sender: Button);
  var локЗаявка : Данные.ЗаявкаНаНазначениеПособия;
  var локЧислоЗаявок : integer;
  var локЗаявкиДляПособия[] : Данные.ЗаявкаНаНазначениеПособия;
    EditorPost;
    локЗаявка = nil;
    локЗаявкиДляПособия =  Данные.интЗаявкаНаНазначениеПособия.ВсеЗаявкиДляНазначенияПособия(Self.record);
    if (LengthOfArray(локЗаявкиДляПособия) = 0) :
      if (ТипПособияПоКодуСО(Self.Record.ВидСтраховогоОбеспечения.Код) = ТипПособияРанСрок) :
        локЗаявка = МенюСоздатьДобавить;
      else
        локЗаявка = ДобавитьНовуюЗаявку;
      fi;
    else
      if (ТипПособияПоКодуСО(Self.Record.ВидСтраховогоОбеспечения.Код) = ТипПособияРанСрок) :
        локЗаявка = МенюОткрытьСоздатьДобавить(локЗаявкиДляПособия);
      else
        локЗаявка = МенюОткрытьСоздать(локЗаявкиДляПособия);
      fi;
    fi;
    if (локЗаявка <> nil) :
      Данные.интЗаявкаНаНазначениеПособия.БланкРедакторПоУмолчанию.ShowFormEx(локЗаявка);
    fi;
    Close;
end;



--}

--{ Вспомогательные методы


 func МенюСоздатьДобавить : Данные.ЗаявкаНаНазначениеПособия;
  var locValuePosition: variant[];
  var p: integer;
  var локЗаявкиДляДобавления[] : Данные.ЗаявкаНаНазначениеПособия;
    Result = nil;
    локЗаявкиДляДобавления = Данные.интЗаявкаНаНазначениеПособия.ЗаявкиДляНазначенияПособияДоп(НашеУчреждение,Сотрудник);
    if (LengthOfArray(локЗаявкиДляДобавления) = 0) :
      Result = ДобавитьНовуюЗаявку;
    else
      locValuePosition[1] = 'Добавить в существующую заявку на пособие по беременности';
      locValuePosition[2] = 'Создать новую заявку';
      p = PopupMenu(locValuePosition);
      if (p = 1) :
        Result = ДобавитьВЗаявку(локЗаявкиДляДобавления);
      elsif (p = 2) :
        Result = ДобавитьНовуюЗаявку;
      fi;
    fi;

 end;

 func МенюОткрытьСоздатьДобавить(локЗаявкиДляПособия[] : Данные.ЗаявкаНаНазначениеПособия) : Данные.ЗаявкаНаНазначениеПособия;
  var locValuePosition: variant[];
  var p: integer;
  var локЗаявкиДляДобавления[] : Данные.ЗаявкаНаНазначениеПособия;
    Result = nil;
    локЗаявкиДляДобавления = Данные.интЗаявкаНаНазначениеПособия.ЗаявкиДляНазначенияПособияДоп(НашеУчреждение,Сотрудник);
    locValuePosition[1] = 'Открыть существующую заявку';
    locValuePosition[2] = 'Создать новую заявку';
    if (LengthOfArray(локЗаявкиДляДобавления) > 0) :
      locValuePosition[3] = 'Добавить в существующую заявку на пособие по беременности';
    fi;
    p = PopupMenu(locValuePosition);
    if (p = 1) :
      Result = ВыбратьЗаявкуФСС(локЗаявкиДляПособия);
    elsif (p = 2) :
      Result = ДобавитьНовуюЗаявку;
    elsif (p = 3) :
      Result =  ДобавитьВЗаявку(локЗаявкиДляДобавления);
    fi;
 end;

 func МенюОткрытьСоздать(локЗаявкиДляПособия[] : Данные.ЗаявкаНаНазначениеПособия) : Данные.ЗаявкаНаНазначениеПособия;
  var locValuePosition: variant[];
  var p: integer;
  var локЗаявкиДляДобавления[] : Данные.ЗаявкаНаНазначениеПособия;
    Result = nil;
    locValuePosition[1] = 'Открыть существующую заявку';
    locValuePosition[2] = 'Создать новую заявку';
    p = PopupMenu(locValuePosition);
    if (p = 1) :
      Result = ВыбратьЗаявкуФСС(локЗаявкиДляПособия);
    elsif (p = 2) :
      Result = ДобавитьНовуюЗаявку;
    fi;

 end;

func ВыбратьЗаявкуФСС (локЗаявкиДляПособия[] : Данные.ЗаявкаНаНазначениеПособия): Данные.ЗаявкаНаНазначениеПособия;
  var локЗаявка : Данные.ЗаявкаНаНазначениеПособия;
  Result = nil;
  if (LengthOfArray(локЗаявкиДляПособия) > 1) :
    if (Данные.картЗаявкаНаНазначениеПособия.ВыполнитьВыбор(локЗаявка, локЗаявкиДляПособия) = CmOk) :
      Result = локЗаявка;
    fi;
  else
    Result = локЗаявкиДляПособия[1];
  fi;
end;

 func ДобавитьНовуюЗаявку : Данные.ЗаявкаНаНазначениеПособия;
   var локПараметры [2] : variant;
   var локНомер : string;
   var локИнт : Данные.интЗаявкаНаНазначениеПособия;
     локПараметры = Данные.интЗаявкаНаНазначениеПособия.ПараметрыНовойЗаявки(Self.Record,nil);
     локИнт = Данные.интЗаявкаНаНазначениеПособия.Create;
     локНомер = ТББ_Базовый.Нумератор.НовыйНомер(локИнт.Record);
     InsertInArray(локПараметры, LengthOfArray(локПараметры) + 1, ['НомерБум', локНомер]);
     InsertInArray(локПараметры, LengthOfArray(локПараметры) + 1, ['Дата', ToDay]);
     локИнт.УстановитьПараметрыНовойЗаписи(локПараметры);
     локИнт.ИнициализироватьПоУмолчанию;
     return локИнт.Record;
 end;

 func ДобавитьВЗаявку(локЗаявкиДляДобавления[] : Данные.ЗаявкаНаНазначениеПособия)  : Данные.ЗаявкаНаНазначениеПособия;
   var локИнт : Данные.интЗаявкаНаНазначениеПособия;
   Result = nil;
   if (Данные.картЗаявкаНаНазначениеПособия.ВыполнитьВыбор(Result, локЗаявкиДляДобавления) = CmOk) :
     локИнт = Данные.интЗаявкаНаНазначениеПособия.ОткрытьПоЗаписи(Result);
     локИнт.ДобавитьНазначениеПособияДоп(Self.Record);
   fi;
 end;


--}
end