extends Бюджет_ЗПиДД.Рабочие.редНазначениеПособия "";

import classes Константы,ФункцииПримененияКостант;--,ФункцииДанных;


inobject private

  var кнЗаявкаФСС : Button;

--------------------------------------------------------------------------------
--{ Обработчики событий бланка

proc шаблон_ПриСоздании (Context: Variant);
  extended шаблон_ПриСоздании(Context);
  if (кнЗаявкаФСС = nil):
    кнЗаявкаФСС = СоздатьДопОбъект(Button);
    кнЗаявкаФСС.Caption = 'Заявка в ТО ФСС';
    кнЗаявкаФСС.Hint = 'Печать типовой формы';
  fi;
end;


  proc шаблон_ПриСчитывании;
    extended  шаблон_ПриСчитывании;
    кнЗаявкаФСС.Visible = false;
    if (Интерфейс.СуммаОплатыФсс <> 0) :
      if (ЭтоПрямыеВыплаты(Self.Record.ВидСтраховогоОбеспечения.Код)) :
        кнЗаявкаФСС.Visible = true;
      fi;
    fi;
  end;

proc кнЗаявкаФСС_ПриНажатии (Sender: Button);
  var локЗаявка : Данные.ЗаявкаНаНазначениеПособия;
    EditorPost;
    локЗаявка = Данные.интЗаявкаНаНазначениеПособия.ЗаявкаДляНазначенияПособия(Self.record);
    if (локЗаявка = nil) :
      if (ТипПособияПоКодуСО(Self.Record.ВидСтраховогоОбеспечения.Код) = ТипПособияУходРеб) :
        локЗаявка = ДобавитьВЗаявку;
      else
        локЗаявка = ДобавитьНовуюЗаявку;
      fi;
    fi;
    if (локЗаявка <> nil) :
      Данные.интЗаявкаНаНазначениеПособия.БланкРедакторПоУмолчанию.ShowFormEx(локЗаявка);
    fi;
    Close;
end;



--}

--{ Вспомогательные методы
 func ДобавитьВЗаявку : Данные.ЗаявкаНаНазначениеПособия;
   
   Result = nil;
 end;

 func ДобавитьНовуюЗаявку : Данные.ЗаявкаНаНазначениеПособия;
   var локПараметры [2] : variant;
   var локНомер : string;
   var локИнт : Данные.интЗаявкаНаНазначениеПособия;
     локПараметры = Данные.интЗаявкаНаНазначениеПособия.ПараметрыНовойЗаявки(Self.Record,nil);
     локИнт = Данные.интЗаявкаНаНазначениеПособия.Create;
     локНомер = ТББ_Базовый.Нумератор.НовыйНомер(локИнт.Record);
     InsertInArray(локПараметры, LengthOfArray(локПараметры) + 1, ['НомерБум', локНомер]);
     InsertInArray(локПараметры, LengthOfArray(локПараметры) + 1, ['Дата', ToDay]);
     локИнт.УстановитьПараметрыНовойЗаписи(локПараметры);
     локИнт.ИнициализироватьПоУмолчанию;
     return локИнт.Record;
 end;


--}
end